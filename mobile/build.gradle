import com.mikepenz.aboutlibraries.plugin.DuplicateMode

def taskRequests = getGradle().getStartParameter().getTaskRequests().toString()
def isFoss = taskRequests.contains("Foss") || taskRequests.contains("foss")

buildscript {
    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath libs.google.services.plugin
        classpath libs.firebase.crashlytics.gradle.plugin
        classpath libs.unmock.plugin
        classpath libs.aboutlibraries.plugin
        classpath libs.missing.metadata.guava.plugin
    }
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-parcelize"
apply plugin: "de.mobilej.unmock"
apply plugin: "com.mikepenz.aboutlibraries.plugin"
apply plugin: "de.jjohannes.missing-metadata-guava"

if (!isFoss) {
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

android {
    useLibrary "org.apache.http.legacy"
    namespace "org.openhab.habdroid"

    defaultConfig {
        applicationId "org.openhab.habdroid"
        minSdkVersion 21
        compileSdk 35
        targetSdkVersion 35
        versionCode 584
        versionName "3.19.3-beta"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "long", "TIMESTAMP", System.getenv("SOURCE_DATE_EPOCH") != null ? System.getenv("SOURCE_DATE_EPOCH").toLong() + "L" : System.currentTimeMillis() + "L"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        debug {
            minifyEnabled false
            pseudoLocalesEnabled true
        }
    }

    flavorDimensions "license", "release"
    productFlavors {
        full {
            dimension "license"
            manifestPlaceholders = [maps_api_key: project.findProperty("mapsApiKey") ?: ""]
        }
        foss { dimension "license" }

        stable { dimension "release" }
        beta {
            dimension "release"
            applicationIdSuffix ".beta"
        }
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            all {
                testLogging {
                    showStackTraces true
                    showCauses true
                    exceptionFormat "full"
                }
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += [
            "-opt-in=kotlinx.coroutines.ObsoleteCoroutinesApi",
            "-opt-in=kotlinx.coroutines.DelicateCoroutinesApi"
        ]
        allWarningsAsErrors = true
    }

    lint {
        abortOnError false
        lintConfig file("lint.xml")
    }

    androidResources { generateLocaleConfig true }

    buildFeatures {
        buildConfig true
        viewBinding true
    }
}

aboutLibraries {
    duplicationMode = DuplicateMode.MERGE
    excludeFields = ["generated"]
}

unMock {
    keepStartingWith "libcore."
    keepStartingWith "android.net.Uri"
    keepAndRename "java.nio.charset.Charsets" to "xjava.nio.charset.Charsets"
}

repositories {
    maven { url "https://maven.fabric.io/public" }
    mavenCentral()
    maven { url "https://jitpack.io" }
    google()
}

dependencies {
    coreLibraryDesugaring libs.desugar.jdk.libs

    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.jdk9
    implementation libs.kotlinx.coroutines.android

    implementation libs.core.ktx
    implementation libs.activity.ktx
    implementation libs.fragment.ktx
    implementation libs.kotlin.stdlib.jdk8

    implementation libs.appcompat
    implementation libs.legacy.support.v4
    implementation libs.recyclerview
    implementation libs.constraintlayout
    implementation libs.preference.ktx
    implementation libs.biometric
    implementation libs.material
    implementation libs.multidex

    implementation libs.work.runtime.ktx
    fullImplementation libs.work.gcm

    implementation libs.security.crypto

    implementation libs.media3.exoplayer
    implementation libs.media3.ui
    implementation libs.media3.exoplayer.hls

    implementation libs.jmdns

    implementation libs.okhttp
    implementation libs.logging.interceptor
    implementation libs.okhttp.sse

    implementation libs.colorpicker
    implementation libs.androidsvg
    implementation libs.appintro
    implementation libs.photoview
    implementation libs.skeletonlayout

    fullImplementation libs.play.services.maps
    fossImplementation libs.osmdroid.android

    implementation libs.material.about.library
    implementation libs.aboutlibraries.core
    implementation libs.aboutlibraries

    implementation libs.androidchart

    implementation platform(libs.firebase.bom)
    fullImplementation libs.firebase.messaging.ktx
    fullImplementation libs.firebase.crashlytics.ktx

    fossImplementation libs.acra.mail
    fossImplementation libs.acra.notification

    testImplementation libs.mockito.core
    testImplementation libs.mockito.kotlin
    testImplementation libs.junit
    testImplementation libs.json
    testImplementation libs.mockwebserver
    testImplementation libs.kotlinx.coroutines.test

    testImplementation libs.powermock.core
    testImplementation libs.powermock.api.mockito2
    testImplementation libs.powermock.module.junit4

    androidTestImplementation(libs.espresso.core) {
        exclude group: "com.android.support", module: "support-annotations"
    }
    androidTestImplementation libs.espresso.intents
    androidTestImplementation(libs.espresso.contrib) {
        exclude group: "com.android.support", module: "support-annotations"
    }
    androidTestImplementation libs.junit.ktx
}
